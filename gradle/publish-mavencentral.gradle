apply plugin: 'maven-publish'
apply plugin: 'signing'

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = libraryGroupId
                artifactId = libraryArtifactId
                version = libraryVersion

                from components.release

                pom {
                    name = libraryName
                    packaging = 'aar'
                    description = libraryDesc
                    url = "https://github.com/lopspower/$libraryName"

                    scm {
                        connection = "scm:git:https://github.com/lopspower/$libraryName"
                        developerConnection = "scm:git:https://github.com/lopspower/$libraryName"
                        url = "https://github.com/lopspower/$libraryName"
                    }
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'lopspower'
                            name = 'Mikhael LOPEZ'
                            email = 'lopez.mikhael@gmail.com'
                            url = 'https://github.com/lopspower'
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                name = "ossrh"
                url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = ossrhUsername
                    password = ossrhPassword
                }
            }
        }
    }
}

signing {
    sign publishing.publications
}